define ([
  'mydash'
  'constants'
  'utilities'
  'extractors'
  'slider'
  'king'
  'rook'
  'knight'
  'pawn'
  'assertions'
], (_, c, u, x, slider-fns, king-fns, rook-fns, knight-fns, pawn-fns, v) ->

  dispatch-table = {}
  dispatch-table[c.king  ] = king-fns
  dispatch-table[c.queen ] = slider-fns
  dispatch-table[c.rook  ] = rook-fns
  dispatch-table[c.bishop] = slider-fns
  dispatch-table[c.knight] = knight-fns
  dispatch-table[c.pawn  ] = pawn-fns

  call = \fn-name \chessman-type ->
    # Start testing mode.
    _.vowStr fn-name
    v.vow-chessman-type chessman-type
    # End testing mode.

    dispatch-table[chessman-type][fn-name]

  checkmate? = (bd, rts, passant, color) ->
    # Start testing mode.
    v.vow-board bd
    v.vow-castling-right-set rts
    v.vow-passant-position passant
    v.vow-color color
    # End testing mode.

    ally-positions = x.chessman-positions-from (bd, color)

    for ally-pos in ally-positions
      potential-moves = getPotentialMoves (bd, ally-pos, rts, passant)

      for potential-mv in potential-moves
        new-bd = move (bd, ally-pos, passant) potential-mv
        return false unless in-check? (new-bd, rts, color)

    true

  contains = (potential-positions, tgt) ->
    _.any (u.same-position? tgt) potential-positions

  getPotentialKingAttacks = (bd, src, rts) ->
    # Start testing mode.
    v.vow-board bd
    v.vow-position src
    v.vow-castling-right-set rts
    # End testing mode.

    type = x.chessman-type-at bd src
    call 'getPotentialKingAttacks' type (bd, src, rts, type)

  getPotentialMoves = (bd, src, rts, passant) ->
    # Start testing mode.
    v.vow-board bd
    v.vow-position src
    v.vow-castling-right-set rts
    v.vow-passant-position passant
    # End testing mode.

    type = x.chessman-type-at bd src
    call 'getPotentialMoves' type (bd, src, rts, passant, type)

  # TODO: Reconsider necessity of castling-rights
  in-check? = (board, rts, color) ->
    # Start testing mode.
    v.vow-board board
    v.vow-castling-right-set rts
    v.vow-color color
    # End testing mode.

    king-pos = king-position-for (board, color)
    vuln-positions = vulnerable-positions-for (board, rts, color)
    contains (vuln-positions, king-pos)

  king-position-for = (board, color) ->
    # Start testing mode.
    v.vow-board board
    v.vow-color color
    # End testing mode.

    king = if u.black? color then c.black-king else c.white-king
    u.to-2d (_.indexOf board king)

  modifyCastlingRights = (bd, src, rts) ->
    # Start testing mode.
    v.vow-board bd
    v.vow-position src
    v.vow-castling-right-set rts
    # End testing mode.

    type = x.chessman-type-at bd src
    call 'modifyCastlingRights' type (rts, src)

  move = (bd, src, passant) -> \tgt ->
    # Start testing mode.
    v.vow-board bd
    v.vow-all-positions [src, tgt]
    v.vow-passant-position passant
    # End testing mode.

    type = x.chessman-type-at bd src
    call 'move' type (bd, src, passant) tgt

  move-legal? = (bd, src, rts, passant) -> \tgt ->
    # Start testing mode.
    v.vow-board bd
    v.vow-all-positions [src, tgt]
    v.vow-castling-right-set rts
    v.vow-passant-position passant
    # End testing mode.

    type  = x.chessman-type-at bd src
    potential-tgts = getPotentialMoves (bd, src, rts, passant)
    
    contains (potential-tgts, tgt)

  query-game-service = ->

  setPassantPosition = (bd, src, tgt) ->
    # Start testing mode.
    v.vow-board bd
    v.vow-all-positions [src, tgt]
    # End testing mode.

    chessman = x.square-at bd src
    type     = x.chessman-type-of chessman
    color    = x.color-of chessman

    call 'setPassantPosition' type (color, src, tgt)

  setPromotionPosition = (bd, src, tgt) ->
    # Start testing mode.
    v.vow-board bd
    v.vow-all-positions [src, tgt]
    # End testing mode.

    chessman = x.square-at bd src
    type     = x.chessman-type-of chessman
    color    = x.color-of chessman

    call 'setPromotionPosition' type (color, tgt)

  # TODO: Reconsider necessity of castling-rights
  vulnerable-positions-for = (board, rts, color) ->
    # Start testing mode.
    v.vow-board board
    v.vow-castling-right-set rts
    v.vow-color color
    # End testing mode.

    foe-color     = u.opposing-color (color)
    foe-positions = x.chessman-positions-from (board, foe-color)

    get-potential-king-attacks = \pos ->
      getPotentialKingAttacks (board, pos, rts)

    _.meld (_.map get-potential-king-attacks foe-positions)

  # Start testing mode.

  test-mode = {
    call
    checkmate?
    getPotentialKingAttacks
    getPotentialMoves
    in-check?
    king-position-for
    modifyCastlingRights
    move
    move-legal?
    query-game-service
    setPassantPosition
    setPromotionPosition
    vulnerable-positions-for
  }

  return test-mode

  # End testing mode.

  # Start production mode.
  # return { query-game-service }
  # End production mode.

)
