define ([
  'base-helpers'
  'constants'
  'constructors'
  'extractors'
  'utilities'
  'stepper'
  'assertions'
], (_, c, g, x, u, stepper-fns, v) ->

  KQ = 'KQ'
  kq = 'kq'
  K  = 'K'
  Q  = 'Q'
  k  = 'k'
  q  = 'q'
  NO = '-'

  gen-pos = g.generate-position

  no-rts = c.no-castling-rts

  king-castling-tgt-files = [2, 6]

  king-castling-tgts = [
    c1 = gen-pos (7, 2)
    g1 = gen-pos (7, 6)
    c8 = gen-pos (0, 2)
    g8 = gen-pos (0, 6)
  ]

  [c1-key, g1-key, c8-key, g8-key] = king-castling-tgts.map JSON.stringify

  possible-rook-paths = [[5, 7], [3, 0], [61, 63], [59, 56]]

  rook-castling-srcs = {}
  rook-castling-srcs[c8-key] = gen-pos (0, 0)
  rook-castling-srcs[g8-key] = gen-pos (0, 7)
  rook-castling-srcs[c1-key] = gen-pos (7, 0)
  rook-castling-srcs[g1-key] = gen-pos (7, 7)

  rook-castling-tgts = {}
  rook-castling-tgts[c8-key] = gen-pos (0, 3)
  rook-castling-tgts[g8-key] = gen-pos (0, 5)
  rook-castling-tgts[c1-key] = gen-pos (7, 3)
  rook-castling-tgts[g1-key] = gen-pos (7, 5)

  t0  = []
  t1  = [c1]
  t2  = [g1]
  t12 = [c1, g1]
  t3  = [c8]
  t4  = [g8]
  t34 = [c8, g8]

  new-castling-rts = {}

  new-castling-rts[c.white] =
    KQkq : kq, KQk : k, KQq : q, KQ : NO, Kkq : kq, Kk : k, Kq : q, K   : NO
    Qkq  : kq, Qk  : k, Qq  : q, Q  : NO, kq  : kq, k  : k, q  : q, '-' : NO

  new-castling-rts[c.black] =
    KQkq : KQ, Qkq : Q, Kkq : K, kq : NO, KQq : KQ, Qq : Q, Kq : K, q   : NO
    KQk  : KQ, Qk  : Q, Kk  : K, k  : NO, KQ  : KQ, Q  : Q, K  : K, '-' : NO 

  castling-tgts = {}

  castling-tgts[c.white] =
    KQkq : t12, KQk : t12, KQq : t12, KQ  : t12
    Qkq  : t1,  Qk  : t1,  Qq  : t1,  Q   : t1
    Kkq  : t2,  Kk  : t2,  Kq  : t2,  K   : t2
    kq   : t0,  k   : t0,  q   : t0,  '-' : t0

  castling-tgts[c.black] =
    KQkq : t34, Qkq : t34, Kkq : t34, kq  : t34
    KQq  : t3,  Qq : t3,   Kq  : t3,  q   : t3
    KQk  : t4,  Qk : t4,   Kk  : t4,  k   : t4
    KQ   : t0,  Q  : t0,   K   : t0,  '-' : t0 

  castling? = (src, tgt) ->
    Math.abs (u.file-difference (src, tgt)) > 1

  castling-permitted? = (castling-rts, black?) -> \rank-diff ->
    kingward? = rank-diff > 0
    matcher = u.castling-matcher (black?, kingward?)
    /#{matcher}/.test castling-rts

  # TODO: Check for non-transitable castling paths.
  # TODO: Check that no pos in the castling path is attackable.
  get-potential-castling-moves = (color, castling-rts) ->
    castling-tgts[color][castling-rts]

  getPotentialKingAttacks = (bd, src) ->
    # Start testing mode.
    v.vow-board bd
    v.vow-position src
    # End testing mode.

    getPotentialMoves (bd, src, no-rts)

  getPotentialMoves = (bd, src, rts) ->
    # Start testing mode.
    v.vow-board bd
    v.vow-position src
    v.vow-castling-right-set rts
    # End testing mode.

    king-color  = x.color-at bd src

    basic-moves = pf.getBasicPotentialMoves (
      bd, src, undefined, undefined, c.king)

    return basic-moves if rts == no-rts

    basic-moves.concat (get-potential-castling-moves (king-color, rts))

  modifyCastlingRights = (rts, src) ->
    # Start testing mode.
    v.vow-castling-right-set rts
    v.vow-position src
    # End testing mode.

    king-color = if x.rank-of src == 0 then c.black else c.white
    new-castling-rts[king-color][rts]

  rook-castling-path = \king-tgt ->
    key = JSON.stringify king-tgt
    src = rook-castling-srcs[key]
    tgt = rook-castling-tgts[key]
    [src, tgt]

  pf = public-fns = _.defaults stepper-fns {
    getPotentialKingAttacks
    getPotentialMoves
    modifyCastlingRights
  }

  castle = do (simple-move = pf.move) ->
    (bd, src, tgt) ->
      [rook-src, rook-tgt] = rook-castling-path tgt
      bd-after-king-moves  = simple-move (bd, src) tgt
      simple-move (bd-after-king-moves, rook-src) rook-tgt
    
  pf.move = do (non-castling-move = pf.move) ->
    (bd, src) -> \tgt ->
      # Start testing mode.
      v.vow-board bd
      v.vow-all-positions [src, tgt]
      # End testing mode.

      if castling? (src, tgt)
        castle (bd, src, tgt)
      else
        non-castling-move (bd, src) tgt

  return public-fns

)
