define (['constants'], (c) ->

  # ------------------------
  # Private Helper Function
  # ------------------------

  has = \owner ->
    fn = Object.hasOwnProperty.bind(owner)
    \prop -> fn prop

  hash? = \val ->
    toString.call val == '[object Object]'

  integer? = \val ->
    val == val >> 0

  satisfies-regex? = \regex \val ->
    if (string? val) then (regex.test val) else false

  string? = \val ->
    toString.call val == '[object String]'

  # -------------------------
  # Public Helper Functions
  # -------------------------

  bounded? = \intg ->
    intg >= 0 && intg <= 7

  empty? = \square ->
    square == c.empty-square

  inessent? = \pos ->
    pos.every bounded?

  # -----------------------
  # Classifying Predicates
  # -----------------------

  black? = \color ->
    color == c.black

  board? = \val ->
    satisfies-regex? /^[kqrbnp-]{64,64}$/i  val

  castling-right? = \val ->
    satisfies-regex? /^[KQkq]$/ val

  castling-right-set? = \val ->
    satisfies-regex? /(^-$)|(^K?Q?k?q?$)/ val

  chart2? = \val ->
    hash? val && do (has = has val) ->
        has 'board' &&
        has 'source' &&
        has 'castlingRights' &&
        has 'passantPosition'

  chessman? = \val ->
    satisfies-regex? /^[kqrbnp]$/i val

  chessman-type? = \val ->
    c.chessman-types.some (\type -> type == val)

  color? = \val ->
    c.colors.some (\color -> color == val)

  color-pair? = \val ->
    pair? val && val.every color?

  context? = \val ->
    board? val['board'] &&
        castling-right-set? val['castlingRights'] &&
        passant-position? val['passantPosition']

  integral-pair? = \val ->
    pair? val && val.every integer?

  pair? = \val ->
    Array.isArray val && val.length == 2

  passant-position? = \pos ->
    unset-passant-position? pos || position? pos

  path? = \val -> 
    pair? val && val.every position?

  pawn-direction? = \val ->
    val == 1 || val == -1

  position? = \val ->
    integral-pair? val && inessent? val

  position-component? = \val ->
    bounded? val && integer? val

  square? = \val ->
    chessman? val || empty? val

  unset-passant-position? = \val ->
    val == c.unset-passant-pos

  return {
    black?
    board?
    bounded?
    castling-right?
    castling-right-set?
    chart2?
    chessman?
    chessman-type?
    color?
    color-pair?
    context?
    empty?
    inessent?
    integral-pair?
    passant-position?
    path?
    pawn-direction?
    position?
    position-component?
    square?
    string?
    unset-passant-position?
  }

)
